/*
********************************************************************************

 SPC Control Limit for JMP - SPC Control Limit evaluation tool

--------------------------------------------------------------------------------
 MIT License

 Copyright (c) 2021 Keiichi Takahashi

 Permission is hereby granted, free of charge, to any person obtaining a copy
 of this software and associated documentation files (the "Software"), to deal
 in the Software without restriction, including without limitation the rights
 to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 copies of the Software, and to permit persons to whom the Software is
 furnished to do so, subject to the following conditions:

 The above copyright notice and this permission notice shall be included in all
 copies or substantial portions of the Software.

 THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 SOFTWARE.
********************************************************************************
*/

// set namespace to local
Names Default To Here( 1 );

// Application Version
app_ver = "0.1";

// dialog for Not Implemented Yet!
notImplemented = Expr(
	New Window( "Sorry!",
		modal,
		V List Box(
			Spacer Box( size( 5, 10 ) ),
			Text Box( "Not Implemented Yet!" ),
			Text Box( " " ),
			H List Box( Spacer Box( size( 120, 0 ) ), but_ok = Button Box( "OK" ), but_ok << set icon( "RangeCheck" ) )
		)
	)
);

/*******************************************************************************
 loadMaster

 description
   Open Master sheet of filename

 arguments
   filename : filename of Excel macro

 return
   data table
 *******************************************************************************/
loadMaster = Function( {filename},
	{Default Local},
	dt = Open(
		filename,
		invisible,
		Worksheets( "Master" ),
		Use for all sheets( 1 ),
		Concatenate Worksheets( 0 ),
		Create Concatenation Column( 0 ),
		Worksheet Settings(
			1,
			Has Column Headers( 1 ),
			Number of Rows in Headers( 1 ),
			Headers Start on Row( 1 ),
			Data Starts on Row( 2 ),
			Data Starts on Column( 1 ),
			Data Ends on Row( 0 ),
			Data Ends on Column( 0 ),
			Replicated Spanned Rows( 1 ),
			Replicated Spanned Headers( 0 ),
			Suppress Hidden Rows( 1 ),
			Suppress Hidden Columns( 1 ),
			Suppress Empty Columns( 0 ),
			Treat as Hierarchy( 0 ),
			Multiple Series Stack( 0 ),
			Import Cell Colors( 0 ),
			Limit Column Detect( 0 ),
			Column Separator String( "-" )
		)
	);
	Return( dt );
);

/*******************************************************************************
 loadPart

 description
   Open specific PART sheet of filename

 arguments
   filename : filename of Excel macro
   partnum  : Part Number

 return
   data table
 *******************************************************************************/
loadPart = Function( {filename, partnum},
	{Default Local},
	dt = Open(
		filename,
		invisible,
		Worksheets( partnum ),
		Use for all sheets( 1 ),
		Concatenate Worksheets( 0 ),
		Create Concatenation Column( 0 ),
		Worksheet Settings(
			1,
			Has Column Headers( 1 ),
			Number of Rows in Headers( 1 ),
			Headers Start on Row( 2 ),
			Data Starts on Row( 3 ),
			Data Starts on Column( 1 ),
			Data Ends on Row( 0 ),
			Data Ends on Column( 0 ),
			Replicated Spanned Rows( 1 ),
			Replicated Spanned Headers( 0 ),
			Suppress Hidden Rows( 1 ),
			Suppress Hidden Columns( 1 ),
			Suppress Empty Columns( 0 ),
			Treat as Hierarchy( 0 ),
			Multiple Series Stack( 0 ),
			Import Cell Colors( 0 ),
			Limit Column Detect( 0 ),
			Column Separator String( "-" )
		)
	);
	Return( dt );
);

/*******************************************************************************
 deleteEmptyRow

 description
   delete rows where specified column is empty 

 arguments
   dt      : data table
   colname : name of column to check if specified row is empty or not

 return
   (none)
 *******************************************************************************/
deleteEmptyRow = Function( {dt, colname},
	{Default Local},
	rows = N Rows( dt );
	For( r = rows, r > 0, r--,
		pn = Column( colname )[r];
		If( pn == "",
			dt << Delete Rows( r )
		);
	);
);

/*******************************************************************************
 concatTwoCols

 description
   Concatenate 2 columns in to one list 

 arguments
   dt       : data table
   colname1 : name of column 1
   colname2 : name of column 2
   sep      : separater, default = 2 spaces

 return
   list of element of colname1 || sep || element of colname2
 *******************************************************************************/
concatTwoCols = Function( {dt, colname1, colname2, sep = "  "},
	{Default Local},
	rows = N Rows( dt );
	elementList = {};
	For( r = 1, r <= rows, r++,
		item1 = Column( dt, colname1 )[r];
		item2 = Column( dt, colname2 )[r];
		//element = name1 || sep || name2;
		element = Concat Items( {item1, item2}, sep );
		Insert Into( elementList, element );
	);
	Return( elementList );
);

/*******************************************************************************
 getCellValue

 description
   Get cell value in specified data table 

 arguments
   dt       : data table
   colname  : NAME of column
   row      : row NUMBER

 return
   velue at specified column name and row
 *******************************************************************************/
getCellValue = Function( {dt, colname, row},
	{Default Local},
	value = Column( dt, colname )[row];
	Return( value );
);

/*******************************************************************************
 spcChart
 
 description
   Plot basic SPC Control Chart
   
 arguments
   dt        : data table
   paramname : PARAMETER NAME
 
 return
   instance of the SPC Control Chart
 *******************************************************************************/
spcChart = Function( {dt, paramname},
	{Default Local},
	obj = Control Chart(
		Group Size( 1 ),
		KSigma( 3 ),
		Chart Col( Column( dt, paramname ) ),
		Chart Type( Individual Measurement ),
		Alarm Script( Write( "\!NOut of Control for test ", qc_test, " in column ", qc_col, " in sample ", qc_sample ) )
	);
	obj << Test 1( 1 );
	obj << Test 2( 1 );
	obj << Test 5( 1 );

	Return( obj );
);

/*******************************************************************************
 spcHist
 
 description
   Data distribution and Normality check
   
 arguments
   dt        : data table
   paramname : PARAMETER NAME
 
 return
   instance of the Distribution report
 *******************************************************************************/
spcHist = Function( {dt, paramname},
	{Default Local}, 
	// https://community.jmp.com/t5/Discussions/How-to-manage-display-hide-Test-n-in-Warnings-of-Control-Chart/td-p/341481
	Eval(
		Substitute(
				Expr(
					obj = __dt__ << Distribution(
						Show Menu( 0 ),
						Show Toolbars( 0 ),
						Continuous Distribution(
							Column( __param__ ),
							Quantiles( 0 ),
							Horizontal Layout( 1 ),
							Vertical( 0 ),
							Count Axis( 1 ),
							Fit Distribution( Normal( Goodness of Fit( 1 ) ) )
						)
					);
					Return( obj );
				),
			Expr( __dt__ ), dt,
			Expr( __param__ ), paramname
		)
	)
);

/*******************************************************************************
 summaryTbl

 description
   generate empty (row = 0) summary table

 arguments
   (none)

 return
   (none) 
 *******************************************************************************/
summaryTbl = Function( {},
	{Default Local},
	obj = New Table( "Summary",
		invisible,
		Add Rows( 0 ),
		New Column( "Part Number", Character, "Nominal", Set Values( {} ) ),
		New Column( "Parameter Name", Character, "Nominal", Set Values( {} ) ),
		New Column( "Distribution", Character, "Nominal", Set Values( {} ) ),
		New Column( "Chart Type", Character, "Nominal", Set Values( {} ) ),
		New Column( "Test 1, 2, 5", Character, "Nominal", Set Values( {} ) ),
		New Column( "Cpk", Numeric, "Continuous", Set Values( [] ) ),
		New Column( "Cp", Numeric, "Continuous", Set Values( [] ) ),
		New Column( "LCL", Numeric, "Continuous", Set Values( [] ) ),
		New Column( "UCL", Numeric, "Continuous", Set Values( [] ) ),
		New Column( "LSL", Numeric, "Continuous", Set Values( [] ) ),
		New Column( "USL", Numeric, "Continuous", Set Values( [] ) )
	);
	Return(obj);
);

/*******************************************************************************
 evalCL

 description
   evaluate control limit

 arguments
   listbox : List Box shown PART NUMBER & PARAMETER NAME

 return
   (none) 
 *******************************************************************************/
evalCL = Function( {listbox},
	{Default Local},
	If( N Items( listbox << Get Selected Indices ) == 0,
		Return()
	); // return if no selection on the List Box

	// get selected information on the List Box
	idx = Eval( listbox << Get Selected Indices )[1];
	report_title = Eval( listbox << Get Selected )[1];

	// Regular Expression to separate PART NUMBER and PARAMETER NAME
	matchList = Regex Match( report_title, "^(.+)\s{2}(.+)$" );
	partnum = matchList[2];
	paramname = matchList[3];

	// Spec Limits
	lsl = getCellValue( Here:dict["Master"], "LSL", idx );
	usl = getCellValue( Here:dict["Master"], "USL", idx );

	// Control Chart
	chart = spcChart( Here:dict[partnum], paramname );
	
	// Histogram
	hist = spcHist( Here:dict[partnum], paramname );
	
	// Summary
	dt_summary = summaryTbl();
	Here:dict["Summary"] = dt_summary;

	dt_summary << Add Rows( 1 );
	Column( dt_summary, "Part Number" )[1] = partnum;
	Column( dt_summary, "Parameter Name" )[1] = paramname;
	Column( dt_summary, "LSL" )[1] = lsl;
	Column( dt_summary, "USL" )[1] = usl;
	
	// Summary Report
	report = New Window( "Summary Report", H List Box( Outline Box( "Summary", tbl = dt_summary << Get As Report ) ), );
);

/*******************************************************************************
 quitApp

 description
   Close application

 arguments
   win  : Application window

 return
   (none) 
 *******************************************************************************/
quitApp = Function( {win},
	{Default Local}, 
	// Close all tables stored in dictionary
	//
	//   reference: https://community.jmp.com/t5/JSL-Cookbook/Looping-through-an-Associative-Array-s-elements/ta-p/236892
	For( dt = Here:dict << first, !Is Empty( dt ), dt = Here:dict << next( dt ),
		Close( dt, "No Save" )
	);
	// Close Application Window
	win << Close Window();
);


/*******************************************************************************
  MAIN
 *******************************************************************************/

// select file to read
filename = Pick File(
	"Select SPC macro file", // prompt message
	"", // initial directory
	{"SPC Macro file|xlsm", "All files|*"}, // file filter list by extention
	1, // intially selected item
	0, // doesn’t prompt the user to save the file
	"" // file that is selected by default
);

// If filename is empty, script is aborted with alert dialog.
If( filename == "",
	Throw( "!Empty Filename." )
);

// Disctonary of data table
//
//   For Associative Array
//   Reference: https://www.jmp.com/support/help/14-2/create-associative-arrays.shtml
dict = Associative Array();

// Open Master sheet of filename and register disctionary
dict["Master"] = loadMaster( filename );

// Delete rows where column 1, "Part Number" is empty 
deleteEmptyRow( dict["Master"], "Part Number" );

// Extract list of unique "Part Number" using Associative Array
//   Note:      Associative Array allows unique keys and this is a kind of
//              technique to extract unique value from list using Associative Array.
//
//   Reference: https://community.jmp.com/t5/JSL-Cookbook/Get-a-list-of-the-unique-values-in-a-column-matrix-or-list/ta-p/51256
partlist_all = Column( dict["Master"], "Part Number" ) << Get Values;
partlist = Associative Array( partlist_all ) << Get Keys;

// Open data sheet and register instance to disctionary
For( i = 1, i <= N Items( partlist ), i++,
	partnum = partlist[i];
	dict[partnum] = loadPart( filename, partnum );
);

// Concatenate 2 columns in to one list
elementList = concatTwoCols( dict["Master"], "Part Number", "Parameter Name" );

// ---------------------------------------------------------------------------
//  Main Window
win_main = New Window( Concat Items( {"SPC Control Limit", app_ver}, " " ),
	Show Menu( 0 ),
	Show Toolbars( 0 ),
	H List Box(
		Spacer Box( size( 5, 0 ) ),
		Panel Box( "Parameter", lbox = List Box( elementList, width( 500 ), nLines( 10 ), max selected( 1 ) ) ),
		Panel Box( "Action",
			Lineup Box( N Col( 1 ), 
				// Evaluate CL button
				but_plot = Button Box( "Eval", evalCL( lbox ) ),
				but_plot << Set Icon( "FunctionalDataExplorer" ),
				but_plot << Set Icon Location( "left" ), 
				// Quit button
				but_quit = Button Box( "Quit", quitApp( win_main ) ),
				but_quit << Set Icon( "Close" ),
				but_quit << Set Icon Location( "left" )
			)
		),
		Spacer Box( size( 5, 0 ) )
	)
);

// ---
// END OF PROGRAM
