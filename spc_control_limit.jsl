/*
********************************************************************************

 SPC Control Limit for JMP - SPC Control Limit evaluation tool

--------------------------------------------------------------------------------
 MIT License

 Copyright (c) 2021 Keiichi Takahashi

 Permission is hereby granted, free of charge, to any person obtaining a copy
 of this software and associated documentation files (the "Software"), to deal
 in the Software without restriction, including without limitation the rights
 to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 copies of the Software, and to permit persons to whom the Software is
 furnished to do so, subject to the following conditions:

 The above copyright notice and this permission notice shall be included in all
 copies or substantial portions of the Software.

 THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 SOFTWARE.
********************************************************************************
*/

// set namespace to Here
// name of 'Here' is JMP recommended name for local namespace
Names Default To Here( 1 );

// Application Version
app_ver = "0.2"; // from 8-Jan-2021

// CONSTANT
strPART = "Part Number";
strPARAM = "Parameter Name";
strDIST = "Distribution";
strCTYPE = "Chart Type";
strTEST125 = "Test 1, 2, 5";
strCPK = "Cpk";
strCP = "Cp";
strLCL = "LCL";
strUCL = "UCL";
strLSL = "LSL";
strUSL = "USL";

// known distionary keys
keyMASTER = "Master";
keySUMMARY = "Summary";

/*
--------------------------------------------------------------------------------
*** v0.2 milestone ***

implement reporting function for all parameters
--------------------------------------------------------------------------------
*/

// dialog for 'Not Implemented Yet'!
notImplemented = Expr(
	New Window( "Sorry!",
		modal,
		V List Box(
			Spacer Box( size( 5, 10 ) ),
			Text Box( "Not Implemented Yet!" ),
			Text Box( " " ),
			H List Box( Spacer Box( size( 120, 0 ) ), but_ok = Button Box( "OK" ), but_ok << set icon( "RangeCheck" ) )
		)
	)
);

/*******************************************************************************
 loadMaster

 description
   Open Master sheet of filename

 arguments
   filename : filename of Excel macro

 return
   data table
 *******************************************************************************/
loadMaster = Function( {filename},
	{Default Local},
	dt = Open(
		filename,
		invisible,
		Worksheets( Here:keyMASTER ),
		Use for all sheets( 1 ),
		Concatenate Worksheets( 0 ),
		Create Concatenation Column( 0 ),
		Worksheet Settings(
			1,
			Has Column Headers( 1 ),
			Number of Rows in Headers( 1 ),
			Headers Start on Row( 1 ),
			Data Starts on Row( 2 ),
			Data Starts on Column( 1 ),
			Data Ends on Row( 0 ),
			Data Ends on Column( 0 ),
			Replicated Spanned Rows( 1 ),
			Replicated Spanned Headers( 0 ),
			Suppress Hidden Rows( 1 ),
			Suppress Hidden Columns( 1 ),
			Suppress Empty Columns( 0 ),
			Treat as Hierarchy( 0 ),
			Multiple Series Stack( 0 ),
			Import Cell Colors( 0 ),
			Limit Column Detect( 0 ),
			Column Separator String( "-" )
		)
	);
	Return( dt );
);

/*******************************************************************************
 loadPart

 description
   Open specific PART sheet of filename

 arguments
   filename : filename of Excel macro
   partnum  : Part Number

 return
   data table
 *******************************************************************************/
loadPart = Function( {filename, partnum},
	{Default Local},
	dt = Open(
		filename,
		invisible,
		Worksheets( partnum ),
		Use for all sheets( 1 ),
		Concatenate Worksheets( 0 ),
		Create Concatenation Column( 0 ),
		Worksheet Settings(
			1,
			Has Column Headers( 1 ),
			Number of Rows in Headers( 1 ),
			Headers Start on Row( 2 ),
			Data Starts on Row( 3 ),
			Data Starts on Column( 1 ),
			Data Ends on Row( 0 ),
			Data Ends on Column( 0 ),
			Replicated Spanned Rows( 1 ),
			Replicated Spanned Headers( 0 ),
			Suppress Hidden Rows( 1 ),
			Suppress Hidden Columns( 1 ),
			Suppress Empty Columns( 0 ),
			Treat as Hierarchy( 0 ),
			Multiple Series Stack( 0 ),
			Import Cell Colors( 0 ),
			Limit Column Detect( 0 ),
			Column Separator String( "-" )
		)
	);
	Return( dt );
);

/*******************************************************************************
 deleteEmptyRow

 description
   delete rows where specified column is empty 

 arguments
   dt      : data table
   colname : name of column to check if specified row is empty or not

 return
   (none)
 *******************************************************************************/
deleteEmptyRow = Function( {dt, colname},
	{Default Local},
	rows = N Rows( dt );
	For( r = rows, r > 0, r--,
		pn = Column( colname )[r];
		If( pn == "",
			dt << Delete Rows( r )
		);
	);
);

/*******************************************************************************
 concatTwoCols

 description
   Concatenate 2 columns in to one list 

 arguments
   dt       : data table
   colname1 : name of column 1
   colname2 : name of column 2
   sep      : separater, default = 2 spaces

 return
   list of element of colname1 || sep || element of colname2
 *******************************************************************************/
concatTwoCols = Function( {dt, colname1, colname2, sep = "  "},
	{Default Local},
	rows = N Rows( dt );
	elementList = {};
	For( r = 1, r <= rows, r++,
		item1 = Column( dt, colname1 )[r];
		item2 = Column( dt, colname2 )[r];
		//element = name1 || sep || name2;
		element = Concat Items( {item1, item2}, sep );
		Insert Into( elementList, element );
	);
	Return( elementList );
);

/*******************************************************************************
 getCellValue

 description
   Get cell value in specified data table 

 arguments
   dt       : data table
   colname  : NAME of column
   row      : row NUMBER

 return
   velue at specified column name and row
 *******************************************************************************/
getCellValue = Function( {dt, colname, row},
	{Default Local},
	value = Column( dt, colname )[row];
	Return( value );
);

/*******************************************************************************
 spcChart
 
 description
   Plot basic SPC Control Chart
   
 arguments
   dt        : data table
   paramname : PARAMETER NAME
 
 return
   instance of the SPC Control Chart
 *******************************************************************************/
spcChart = Function( {dt, paramname},
	{Default Local},
	obj = Control Chart(
		Group Size( 1 ),
		KSigma( 3 ),
		Chart Col( Column( dt, paramname ) ),
		Chart Type( Individual Measurement ),
		Alarm Script( Write( "\!NOut of Control for test ", qc_test, " in column ", qc_col, " in sample ", qc_sample ) )
	);
	obj << Test 1( 1 );
	obj << Test 2( 1 );
	obj << Test 5( 1 );

	Return( obj );
);

/*******************************************************************************
 spcHist
 
 description
   Data distribution and Normality check
   
 arguments
   dt        : data table
   paramname : PARAMETER NAME
 
 return
   instance of the Distribution report
 *******************************************************************************/
spcHist = Function( {dt, paramname},
	{Default Local}, 
	// https://community.jmp.com/t5/Discussions/How-to-manage-display-hide-Test-n-in-Warnings-of-Control-Chart/td-p/341481
	Eval(
		Substitute(
				Expr(
					obj = __dt__ << Distribution(
						Show Menu( 0 ),
						Show Toolbars( 0 ),
						Continuous Distribution(
							Column( __param__ ),
							Quantiles( 0 ),
							Vertical( 0 ),
							Count Axis( 1 ),
							Fit Distribution( Normal( Goodness of Fit( 1 ) ) )
						),
						SendToReport(
							Dispatch( {__param__}, "Summary Statistics", OutlineBox, {Close( 1 )} ),
							Dispatch( {__param__, "Fitted Normal"}, "Parameter Estimates", OutlineBox, {Close( 1 )} )
						)
					);
					Return( obj );
				),
			Expr( __dt__ ), dt,
			Expr( __param__ ), paramname
		)
	)
);

/*******************************************************************************
 summaryTbl

 description
   generate empty (row = 0) summary table

 arguments
   (none)

 return
   (none) 
 *******************************************************************************/
summaryTbl = Function( {},
	{Default Local},
	obj = New Table( Here:keySUMMARY,
		invisible,
		Add Rows( 0 ),
		New Column( Here:strPART, Character, "Nominal", Set Values( {} ) ),
		New Column( Here:strPARAM, Character, "Nominal", Set Values( {} ) ),
		New Column( Here:strDIST, Character, "Nominal", Set Values( {} ) ),
		New Column( Here:strCTYPE, Character, "Nominal", Set Values( {} ) ),
		New Column( Here:strTEST125, Character, "Nominal", Set Values( {} ) ),
		New Column( Here:strCPK, Numeric, "Continuous", Set Values( [] ) ),
		New Column( Here:strCP, Numeric, "Continuous", Set Values( [] ) ),
		New Column( Here:strLCL, Numeric, "Continuous", Set Values( [] ) ),
		New Column( Here:strUCL, Numeric, "Continuous", Set Values( [] ) ),
		New Column( Here:strLSL, Numeric, "Continuous", Set Values( [] ) ),
		New Column( Here:strUSL, Numeric, "Continuous", Set Values( [] ) )
	);
	Return( obj );
);

/*******************************************************************************
 evalCL

 description
   evaluate control limit

 arguments
   listbox : List Box shown PART NUMBER & PARAMETER NAME

 return
   (none) 
 *******************************************************************************/
evalCL = Function( {listbox},
	{Default Local}, 

	listIndex = listbox << Get Selected Indices;
	listItem = listbox << Get Selected;
	Print( listItem );

	// check number of selection on the listbox
	If( N Items( listItem ) == 0,
		Return()
	); // return if no selection on the List Box

	// Empty Windows for Analysis Report
	report_analysis = New Window( "Analysis Report", Show Menu( 0 ), Show Toolbars( 0 ), tbox = Tab Box() );

	For( i = 1, i <= N Items( listItem ), i++, 
		// get selected information on the List Box
		idx = listIndex[i];
		report_title = listItem[i];
		Print( idx );
		Print( report_title );

		// Regular Expression to separate PART NUMBER and PARAMETER NAME
		matchList = Regex Match( report_title, "^(.+)\s{2}(.+)$" );
		partnum = matchList[2];
		paramname = matchList[3];

		//_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
		// Control Chart
		//
		// with lazy evaluation using Expr
		chart = Expr(
			spcChart( Here:dict[partnum], paramname )
		);

		//_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
		// Histogram
		//
		// with lazy evaluation using Expr
		hist = Expr(
			spcHist( Here:dict[partnum], paramname )
		);

		//_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
		// Combine Chart & Histogram
		//
		// reference:
		// https://community.jmp.com/t5/Discussions/For-Loop-inside-a-tab-box-where-each-loop-creates-a-different/td-p/83805
		//tb << Add( Tab Page Box( report_title, H List Box( chart, hist ) ) );	
		tbox << Add( report_title, H List Box( chart, hist ) );	
	);

	tbox << Set Selected(1),

	//_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
	// Summary Report
	//report_summary = New Window( "Summary Report",
	//	Show Menu( 0 ),
	//	Show Toolbars( 0 ),
	//	H List Box( Outline Box( "Summary", tbl = Here:dt_summary << Get As Report ) ), 

	//);
);

/*******************************************************************************
 mainWin

 description
   Generate Main GUI Window

 arguments
   dispList  : list of pair of PART NUMBER & PARAMETER

 return
   instance of this window 
 *******************************************************************************/
mainWin = Function( {dispList},
	{Default Local},
	Here:mw = New Window( Concat Items( {"SPC Control Limit", Here:app_ver}, " " ),
		Show Menu( 0 ),
		Show Toolbars( 0 ),
		H List Box(
			Spacer Box( size( 5, 0 ) ),
			Panel Box( "Parameter", Here:lbox = List Box( dispList, width( 500 ), nLines( 10 ), max selected( N Items( dispList ) ) ) ),
			Panel Box( "Action",
				Lineup Box( N Col( 1 ), 
					// Evaluate CL button
					but_plot = Button Box( "Eval", evalCL( Here:lbox ) ),
					but_plot << Set Icon( "FunctionalDataExplorer" ),
					but_plot << Set Icon Location( "left" ), 
					// Quit button
					but_quit = Button Box( "Quit", quitApp( Here:mw ) ),
					but_quit << Set Icon( "Close" ),
					but_quit << Set Icon Location( "left" )
				)
			),
			Spacer Box( size( 5, 0 ) )
		)
	);
	Return( Here:mw );
);

/*******************************************************************************
 quitApp

 description
   Close application

 arguments
   win  : Application window

 return
   (none) 
 *******************************************************************************/
quitApp = Function( {win},
	{Default Local}, 
	// Close all tables stored in dictionary
	//
	//   reference: https://community.jmp.com/t5/JSL-Cookbook/Looping-through-an-Associative-Array-s-elements/ta-p/236892
	For( dt = Here:dict << first, !Is Empty( dt ), dt = Here:dict << next( dt ),
		Close( dt, "No Save" )
	);
	// Close Application Window
	win << Close Window();
);

/*******************************************************************************
  MAIN (Namespace: Here)
 *******************************************************************************/

// select file to read
filename = Pick File(
	"Select SPC macro file", // prompt message
	"", // initial directory
	{"SPC Macro file|xlsm", "All files|*"}, // file filter list by extention
	1, // intially selected item
	0, // doesn’t prompt the user to save the file
	"" // file that is selected by default
);

// If filename is empty, script is aborted with alert dialog.
If( filename == "",
	Throw( "!Empty Filename." )
);

// Disctonary of data table
//
//   For Associative Array
//   Reference: https://www.jmp.com/support/help/14-2/create-associative-arrays.shtml
dict = Associative Array();

// Open Master sheet of filename and register disctionary
dict[keyMASTER] = loadMaster( filename );

// Delete rows where column 1, "Part Number" is empty 
deleteEmptyRow( dict[keyMASTER], strPART );

// Extract list of unique "Part Number" using Associative Array
//   Note:      Associative Array allows unique keys and this is a kind of
//              technique to extract unique value from list using Associative Array.
//
//   Reference: https://community.jmp.com/t5/JSL-Cookbook/Get-a-list-of-the-unique-values-in-a-column-matrix-or-list/ta-p/51256
partlist_all = Column( dict[keyMASTER], strPART ) << Get Values;
partlist = Associative Array( partlist_all ) << Get Keys;

// Open data sheet and register instance to disctionary
For( i = 1, i <= N Items( partlist ), i++,
	partnum = partlist[i];
	dict[partnum] = loadPart( filename, partnum );
);

// Concatenate 2 columns in to one list
elementList = concatTwoCols( dict[keyMASTER], strPART, strPARAM );


//  Main GUI Window
win_main = mainWin( elementList );

// ---
// END OF PROGRAM
